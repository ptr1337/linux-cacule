pkgbase=linux-cacule-llvm
pkgname=("${pkgbase}" "${pkgbase}-headers")
pkgver=5.13.4
pkgrel=1
arch=(x86_64 x86_64_v3)
pkgdesc='Linux Kernel with cacule scheduler and lto compiled'
_gittag=v${pkgver%.*}-${pkgver##*.}
url="https://kernel.org/"
license=(GPL2)
makedepends=(
  bc kmod libelf pahole cpio perl tar xz
  xmlto python-sphinx python-sphinx_rtd_theme
  graphviz imagemagick git
)
_patchsource="https://raw.githubusercontent.com/ptr1337/linux-cacule-aur/master/patches/5.13"
_caculepatches="https://raw.githubusercontent.com/ptr1337/linux-cacule-aur/master/patches/CacULE"
source=(
  "https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${pkgver}.tar"{.xz,.sign}
  'config' # kernel config file
  '0002-x86-fpu-2021-07-07.patch'
  '0003-clang.patch'
  "${_patchsource}/arch-patches/0001-ZEN-Add-sysctl-and-CONFIG-to-disallow-unprivileged-C.patch"
  "${_caculepatches}/v5.13/cacule-5.13.patch"
  "${_patchsource}/cpu-patches/0001-cpu-patches.patch"
  "${_patchsource}/futex-patches/0001-futex-resync-from-gitlab.collabora.com.patch"
  "${_patchsource}/futex2/0007-v5.13-futex2_interface.patch"
  "${_patchsource}/winesync/5.13-winesync.patch"
  "${_patchsource}/zen-patches/0001-zen-patches.patch"
  "${_patchsource}/lqx-patches/0001-zen-Allow-MSR-writes-by-default.patch"
  "${_patchsource}/bfq-patches-v3/0001-bfq-patches.patch"
  "${_patchsource}/block-patches-v2/0001-block-patches.patch"
  "${_patchsource}/fixes-miscellaneous/0001-fixes-miscellaneous.patch"
  "${_patchsource}/bbr2-patches-v2/0001-bbr2-patches.patch"
  "${_patchsource}/btrfs-patches-v2/0001-btrfs-patches.patch"
  "${_patchsource}/android-patches/0001-android-export-symbold-and-enable-building-ashmem-an.patch"
  "${_patchsource}/pf-patches-v5/0001-pf-patches.patch"
  "${_patchsource}/lru-patches-v5/0001-lru-patches.patch"
#  "${_patchsource}/lru-patches/lru_5.13.patch"
#  "${_patchsource}/lru-patches/le9db_patches/le9db1-5.10.patch"
  "${_patchsource}/ntfs3-patches/0001-ntfs3-patches.patch"
  "${_patchsource}/misc/nohzfull.patch"
  "${_patchsource}/alsa-patches/0001-alsa-patches.patch"
  "${_patchsource}/zstd-upstream-patches/0001-zstd-upstream-patches.patch"
  "${_patchsource}/clearlinux-patches-v2/0001-clearlinux-patches.patch"
  "${_patchsource}/v4l2loopback-patches/0001-v4l2loopback-patches.patch"

)
BUILD_FLAGS=(
      LLVM=1
      LLVM_IAS=1
      CC=clang
      CXX=clang++
      LD=ld.lld
      AR=llvm-ar
      NM=llvm-nm
      STRIP=llvm-strip
      READELF=llvm-readelf
      HOSTCC=clang
      HOSTCXX=clang++
      HOSTAR=llvm-ar
      HOSTLD=ld.lld
      OBJCOPY=llvm-objcopy
      OBJDUMP=objdump
    )
sha256sums=('7192cd2f654aa6083451dea01b80748fe1eebcf2476a589ef4146590030e7d6c'
            'SKIP'
            '8342cbda2e7b7919eb0177dd0a8bfdcfe6ab8438d6da488941095206678fd555'
            'af2f2bdac87f888f1cf363b1962f6ea83479c4e09e240f1745d344b89f8e383a'
            'f1e6b22b0d98c512b23dba9126d86bc4112c8b8a2958252d80ea2bd36a7e9cf2'
            '34614e92ed29d11f5f6150ee8ed6c5ffe7f8f3d99a2fed6aebe40e513749c3ba'
            '7acb2cbcbf82833842c1835828e3e9404d7e6b461657dbd0e51421e7cd106c96'
            '476c99cb010eb536ebf8b68044cd7f2a581c74e4d5c5e71e0983541f727bafde'
            'a65035f7b751ea792989784083d5063293d1a0979bcf4c428b4ba94aeac17809'
            '9ec679871cba674cf876ba836cde969296ae5034bcc10e1ec39b372e6e07aab0'
            '034d12a73b507133da2c69a34d61efd2f6b6618549650aa26d748142d22002e1'
            'f39ce0a6a967e4c83f665288479c3236b211bbbb4ee508d6fbefee2904a4e80c'
            '8db201cee232333287b8557456577a0151a7208481af866dbacd08943ac29aed'
            'c5501f058a8accf538fdb9cc541bd08419cd4d597e2c5bc31365d70c68bba5b3'
            '0735544a91293d9c192b7f9283541fe62ea5517c11e4b421b502ab76c064bd62'
            '320e67ab827abb506481b9053fae85e494195e5d0ee3b61948999965856b425c'
            '744a615a9099df44bb9c181f1d140a099fe11136c8dbb0b26e4af045460298a6'
            'aa5cbec74dc27591d47616c6c0748475ab55b5efdcbc9d81c2cc49b9b0bf2c00'
            '7289f4ce29d653a0ca511410b66bd3cb42fbcc54f5e097c85e528d4e27ce41ff'
            'a6c94bc17ca469f9fc3b887f8a6efb8a618c475403b89f016e774832c5156a1c'
            'e33908a2aca1b5b52609075c32c714e4c3f6b52c054da2b2982a86f6f3c4b87a'
            '0a32d43109ab235ac4d49b28af0c5a3e1653a2581facd4595f098977bf19dd80'
            'a13f7e04e264108d642fc968f42d265a7fb7ef2b6e3a28926326882109f5577a'
            '8e56f88209ec69bf7004c52a7f31ba5fffa2c6af3db306e7ec385210a0b5944e'
            '78b07f9d39573633ac7035201d7a95c44675084562995b7e60e549e44fbcfcb7'
            '04205c627cd3dcb737bd7b432cd7172d30f4ca0114b003bc3ac0dc8dadfa3c01'
            '586d03baec3f8b583b1b73e838f5e69c1d25c502e4d7e6738e1291de1b52af15')
options=('!strip')

export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER="${pkgbase}"
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {

  cd "${srcdir:?}/linux-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/linux-${pkgver} directory! Prepare failed! \E[0m"
    exit 1
  )

    ### Setting version
    echo "Setting version..."
    scripts/setlocalversion --save-scmversion
    echo "-$pkgrel" > localversion.10-pkgrel
    echo "${pkgbase#linux}" > localversion.20-pkgname

    local src
        for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch -Np1 < "../$src"
    done

  echo "Setting config..."
  cp ../config .config

#  if [[ "${CFLAGS=}" =~ znver2 ]]; then
#    sed -i -e 's/CONFIG_GENERIC_CPU3/CONFIG_MZEN2/g' .config
#  fi

  make olddefconfig

  make -s kernelrelease >version
  echo "Prepared ${pkgbase} version $(<version)"

}

build() {

  cd "${srcdir:?}/linux-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/linux-${pkgver} directory! Build failed! \E[0m"
    exit 1
  )

 make ${BUILD_FLAGS[*]} all

}

package_linux-cacule-llvm() {

  pkgdesc="The ${pkgdesc} and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('crda: to set the correct wireless channels of your country'
    'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
  replaces=(virtualbox-guest-modules-arch wireguard-arch)

  cd "${srcdir:?}/linux-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/linux-${pkgver} directory! Package linux kernel failed! \E[0m"
    exit 1
  )

  local kernver="$(<version)"
  local modulesdir="${pkgdir:?}/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "${modulesdir}/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "${pkgbase}" | install -Dm644 /dev/stdin "${modulesdir}/pkgbase"

  echo "Installing modules..."
  make INSTALL_MOD_PATH="${pkgdir:?}/usr" INSTALL_MOD_STRIP=1 modules_install

  # remove build and source links
  rm "${modulesdir}/"{source,build}

}

package_linux-cacule-llvm-headers() {

  pkgdesc="Headers and scripts for building modules for the ${pkgdesc}"
  depends=("linux-cacule-llvm=${pkgver}" pahole)

  cd "${srcdir:?}/linux-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/linux-${pkgver} directory! Package linux headers failed! \E[0m"
    exit 1
  )

  local builddir="${pkgdir:?}/usr/lib/modules/$(<version)/build"

  echo "Installing build files..."
  install -Dt "${builddir}" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux
  install -Dt "${builddir}/kernel" -m644 kernel/Makefile
  install -Dt "${builddir}/arch/x86" -m644 arch/x86/Makefile
  cp -t "${builddir}" -a scripts

  # add objtool for external module building and enabled VALIDATION_STACK option
  install -Dt "${builddir}/tools/objtool" tools/objtool/objtool

  # add xfs and shmem for aufs building
  mkdir -p "${builddir}"/{fs/xfs,mm}

  echo "Installing headers..."
  cp -t "${builddir}" -a include
  cp -t "${builddir}/arch/x86" -a arch/x86/include
  install -Dt "${builddir}/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  install -Dt "${builddir}/drivers/md" -m644 drivers/md/*.h
  install -Dt "${builddir}/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "${builddir}/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "${builddir}/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "${builddir}/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "${builddir}/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "${builddir}/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  echo "Installing KConfig files..."
  find . -name 'Kconfig*' -exec install -Dm644 {} "${builddir}/{}" \;

  echo "Removing unneeded architectures..."
  local arch
  for arch in "${builddir}"/arch/*/; do
    [[ $arch = */x86/ ]] && continue
    echo "Removing $(basename "$arch")"
    rm -r "$arch"
  done

  echo "Removing documentation..."
  rm -r "${builddir}/Documentation"

  echo "Removing broken symlinks..."
  find -L "${builddir}" -type l -printf 'Removing %P\n' -delete

  echo "Removing loose objects..."
  find "${builddir}" -type f -name '*.o' -printf 'Removing %P\n' -delete

  echo "Stripping build tools..."
  local file
  while read -rd '' file; do
    case "$(file -bi "$file")" in
    application/x-sharedlib\;*) # Libraries (.so)
      strip -v "${STRIP_SHARED}" "$file" ;;
    application/x-archive\;*) # Libraries (.a)
      strip -v "${STRIP_STATIC}" "$file" ;;
    application/x-executable\;*) # Binaries
      strip -v "${STRIP_BINARIES}" "$file" ;;
    application/x-pie-executable\;*) # Relocatable binaries
      strip -v "${STRIP_SHARED}" "$file" ;;
    esac
  done < <(find "${builddir}" -type f -perm -u+x ! -name vmlinux -print0)

  echo "Stripping vmlinux..."
  strip -v "${STRIP_STATIC}" "${builddir}/vmlinux"

  echo "Adding symlink..."
  mkdir -p "${pkgdir:?}/usr/src"
  ln -sr "${builddir}" "${pkgdir:?}/usr/src/${pkgbase}"

}
